repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply from: file('gradle/idea.gradle')

ext.antlr = [
	antlrSource: 'src/main/antlr',
	destinationDir: 'src/generated-sources/antlr',
	grammarpackage: 'com.devsmart.staple'
]

configurations {
    antlr4 {
        description = "ANTLR4"
    }
}

dependencies {
	compile 'org.antlr:antlr4-runtime:4.2'
	compile 'org.antlr:stringtemplate:4.0.2'
    compile 'com.google.guava:guava:17.0'
	antlr4 group: 'org.antlr', name: 'antlr4', version: '4.2'
	testCompile 'junit:junit:4.11'
}


task antlrOutputDir << {
	mkdir(antlr.destinationDir)
}

task generateGrammarSource(dependsOn: antlrOutputDir, type: JavaExec) {
    description = 'Generates Java sources from ANTLR4 grammars.'

    inputs.dir file(antlr.antlrSource)
    outputs.dir file(antlr.destinationDir)

    def grammars = fileTree(antlr.antlrSource).include('**/*.g4')

    main = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4
    def pkg = antlr.grammarpackage.replaceAll("\\.", "/")
    args = ["-o", "${antlr.destinationDir}/${pkg}"/*, "-atn"*/, "-visitor", "-package", antlr.grammarpackage, grammars.files].flatten()

}

compileJava {
    dependsOn generateGrammarSource
    source antlr.destinationDir
}

clean {
    delete antlr.destinationDir
}