plugins {
    id 'cpp-application'
}


task genLexer(type: Exec) {
    def inputfile = file('src/staple_lex.l')
    def outputfile = file("${buildDir}/generatedSrc/staple_lex.cpp")

    inputs.file(inputfile)
    outputs.file(outputfile)

    executable 'flex'

    args '-o', "${outputfile}", "${inputfile}"
}

task genParser(type: Exec) {

    ext.headerDir = file("${buildDir}/generatedHeaders")

    def parser_input_file = file('src/staple_parser.y')
    def parser_src_file = file("${buildDir}/generatedSrc/staple_parser.cpp")


    inputs.file( parser_input_file )
    outputs.file (parser_src_file)
    outputs.file( "${buildDir}/generatedSrc/staple_parser.hpp" )
    outputs.dir(headerDir)

    executable 'bison'

    args '-d', '-o', "${parser_src_file}", "${parser_input_file}"
}

application {
    source {
        from fileTree('src').matching {
            include '*.cpp'
        }
        from genParser
        from genLexer
    }

    privateHeaders {
        from genParser.headerDir
    }
}